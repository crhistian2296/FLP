#lang eopl
; Autor: Crhistian Alexander Garcia
; Código: 1832124 


;=====================================================================================

;ESPECIFICACION LEXICA

(define lexica
  '((white-sp (whitespace) skip)
    (comment ("//" (arbno (not #\newline))) skip) ;Comentario basado C++
    (string ("'" letter (arbno (or letter digit)) "'") string) ;String basado en python3
    (identifier ("$" letter (arbno (or letter digit))) symbol) ;Identificador basado en PHP
    (number (digit (arbno digit)) number)
    (number ("-" digit (arbno digit)) number)
    (number (digit (arbno digit) "." digit (arbno digit)) number)
    (number ("-" digit (arbno digit) "." digit (arbno digit)) number)))

;=====================================================================================

;ESPECIFICACION GRAMATICAL

(define gramatica
  '(
    (program (expression) run)
    (expression (number) num)
    (expression (string) string)
    (expression (identifier) id)
    (expression ("true")true-exp)
    (expression ("false")false-exp)
    
    ;Condicionales:
    (expression ("if" "(" expression ")" "{" expression "}" "else" "{" expression "}")if-condition); condicional basado en C++

    ;Constantes:
    (exp ("const" identifier "=" expression) constant);expresion basada en C++

    ;Valores definidos:
    (exp ("val-define" identifier "=" expression) unique-var-define) ;estructura basada en C++

    ;Valores indefinidos:
    (exp ("val-undefine" identifier) unique-var-undefine) ;estructura basada en C++
    
    ;Declaracion
    (expression ("declare" "(" (separated-list exp ",") ")" "{"expression"}") declare-exp) ;estructura similar a la declaracion en ruby
    ;Procedimientos:
    (expression ("function" "(" (separated-list identifier ",") ")" "{" expression "}")function-exp); estructura basada en C#

    ;Asignacion:
    (expression ("assign" identifier "=" expression) unique-assign-exp)  ;termino assing extraido de python

    ;Listas:
    (expression ("list" "<"(separated-list expression ",")">" )list-exp) ;estructura basada en C#
    
    ;Evaluar procedimiento:
    (expression
     ("[" expression (separated-list expression ",") "]")app-exp) ;llamado de funciones similar a python

    ;Recusividad:
    (expression       ;long: termino extraido de c++
     ("long" "(" (arbno "const" identifier "(" (separated-list identifier ",")")" "{" expression"}")")""<"expression">")long-exp)

    ;Begin;
    (expression ("{-" expression (arbno ";" expression) "-}") seq-exp)

    ;Primitivas:
    (expression ("(" expression primitive-binary expression ")") bin-primitive-exp)  ;binaria
    (expression (primitive-unary "("expression")") un-primitive-exp)  ;unaria
    (expression ("|(" expression primitive-binary-base expression ")")bin-primitive-base-exp)  ;binaria base distinta
    (expression (primitive-unary-base "(" expression ")") un-primitive-base-exp)  ;unaria base distinta
    (expression ("*(" expression primitive-binary-bool expression ")")bin-primitive-bool-exp)  ;binaria booleana
    (expression (primitive-unary-bool "("expression ")")un-primitive-bool-exp)  ;primitiva unaria booleana
    
    ;Primitivas -> Listas:
    (expression (primitive-pred-list expression)predicate-primitive-list)
    (expression (primitive-binary-list "(" expression expression ")")bin-primitive-list-exp)
    (expression (primitive-unary-list expression)un-primitive-list-exp)
    (expression ("empty")primitive-empty-list)
    (expression (primitive-unary-string "("expression")")un-primitive-string-exp)
    (expression ("#(" expression primitive-binary-string expression ")")bin-primitive-string-exp)
    
    ;Primitivas para enteros:
    (primitive-binary ("+") add-prim)
    (primitive-binary ("-") subtract-prim)
    (primitive-binary ("*") multiply-prim)
    (primitive-binary ("%") module-prim)
    (primitive-binary ("/") div-prim)
    (primitive-unary ("add1") incr-prim)
    (primitive-unary ("sub1") decr-prim)

    ;Distintas bases:
    (expression ( "x32" "["(arbno number) "]") 32base)
    (expression ( "x16" "["  (arbno number) "]")16base)
    (expression ( "x8""["  (arbno number) "]") 8base)
    
    ;Primitivas para base8, base16 y base32:
    (primitive-binary-base ("+") add-prim-base)
    (primitive-binary-base ("-") subtract-prim-base)
    (primitive-binary-base ("*")multiply-prim-base)
    (primitive-unary-base ("|add1") incr-prim-base)
    (primitive-unary-base ("|sub1") decr-prim-base)
    
    ;Primitivas para booleanos:
    (primitive-binary-bool ("<") smaller-than-prim)
    (primitive-binary-bool (">") bigger-than-prim)
    (primitive-binary-bool ("<=")less-or-equal-prim)
    (primitive-binary-bool (">=")bigger-or-equal-prim)
    (primitive-binary-bool ("==")equal-prim)
    (primitive-binary-bool ("!=")different-prim)
    (primitive-binary-bool ("and")and-prim)
    (primitive-binary-bool ("or")or-prim)
    (primitive-unary-bool ("not") not-prim)

    ;Primitivas para cadenas
    (primitive-unary-string ("length") length-prim)
    (primitive-binary-string ("*append")append-string-prim)
    
    ;Primitivas para listas 
    (primitive-pred-list ("empty?") empty?-prim)
    (primitive-pred-list ("list?")list?-prim)
    (primitive-binary-list ("cons") cons-prim)
    (primitive-binary-list ("append") append-prim)
    (primitive-unary-list ("car") car-prim)
    (primitive-unary-list ("cdr")cdr-prim)))

;=====================================================================================
    
;Construidos automaticamente

(sllgen:make-define-datatypes lexica gramatica)

(define show-the-datatypes
  (lambda () (sllgen:list-define-datatypes lexica gramatica)))

(sllgen:make-stream-parser lexica gramatica)

;=====================================================================================

;Parser y Scanner 

(define just-parse
  (sllgen:make-string-parser lexica gramatica))

(define just-scan
  (sllgen:make-string-scanner lexica gramatica))

;=====================================================================================

;El Interpretador (FrontEnd + Evaluacion + señal para lectura )

(define interpreter
  (sllgen:make-rep-loop
   "==>"
   (lambda (pgm) (eval-program pgm))
   (sllgen:make-stream-parser lexica gramatica)))

(define eval-program
  (lambda (pgm)
    (cases program pgm
      (run (body)
      (eval-expression body (empty-env))) )))

;=====================================================================================

;Tipo de dato procval 
(define-datatype procval procval?
  (closure
   (ids (list-of symbol?))
   (body expression?)
   (env environment?)))

;apply-procedure: evalua el cuerpo de un procedimientos en el ambiente extendido correspondiente
(define apply-procedure
  (lambda (proc args)
    (cases procval proc
      (closure (ids body env)
               (eval-expression body (extend-env (make-const-list (length ids)) ids args env))))))

;make-const-list
(define make-const-list
  (lambda (n)
    (if (zero? n) empty (cons "const" (make-const-list (- n 1))))))

;=====================================================================================

;eval-exppression:  Evalua la expresion en el ambiente de entrada

(define eval-expression
  (lambda (exp env)
    (cases expression exp
      (num (number) number)
      (string (string) string)
      (id (id) (apply-env env id))
      (true-exp ()#t)
      (false-exp () #f)
      (bin-primitive-exp (first-exp operation second-exp)
                (eval-ope-bin (eval-expression first-exp env)
                              operation
                              (eval-expression second-exp env)))
      (un-primitive-exp (operation expression)
                        (eval-ope-un operation (eval-expression expression env)))
      (bin-primitive-base-exp(first-exp operation second-exp)
                         (eval-ope-bin-base (eval-expression first-exp env)
                                            operation
                                            (eval-expression second-exp env)))
      (un-primitive-base-exp (operation expression)
                             (eval-ope-un-base operation (eval-expression expression env)))
      (bin-primitive-bool-exp(first-exp operation second-exp)
                             (eval-ope-bin-bool (eval-expression first-exp env)
                                                operation
                                                (eval-expression second-exp env)))
      (un-primitive-bool-exp(operation expression)
                            (eval-ope-un-bool (eval-expression expression env)))
      (predicate-primitive-list (operation expression)
                                (eval-pred-prim-list operation (eval-expression expression env)))
      (bin-primitive-list-exp (operation first-exp second-exp)
                (eval-bin-prim-list (eval-expression first-exp env)operation (eval-expression second-exp env)))
      (un-primitive-list-exp (operation expression)
                             (eval-un-prim-list operation (eval-expression expression)))
      (primitive-empty-list () empty)
      (un-primitive-string-exp (operation expression)
                               (eval-un-prim-string operation (eval-expression expression env)))
      (bin-primitive-string-exp (operation first-exp second-exp)
                                (eval-bin-prim-string (eval-expression first-exp env)
                                                      operation
                                                      (eval-expression second-exp env)))
      (32base (list-of-numbers)
              (eval-base list-of-numbers 32))
      (16base (list-of-numbers)
            (eval-base list-of-numbers 16))
      (8base (list-of-numbers)
           (eval-base list-of-numbers 8))

      (if-condition (test-exp true-exp false-exp)
                    (if (eval-expression test-exp env)
                        (eval-expression true-exp env)
                        (eval-expression false-exp env)))

      (declare-exp (defs body)
                 (let
                     ((consts (exp->const defs))
                       (ids (exp->ids defs))
                       (vals (eval-let-exp (exp->vals defs) env) ))
                     (eval-expression body (extend-env consts ids vals env))))

      (function-exp (ids body)
                            (closure ids body env))
      (unique-assign-exp (id exp)
                (cond
                  [(and(not (equal? (apply-env env id) "#undefine#")) (equal? (apply-env-const env id) "unique-val"))
                    (begin
                      (setref!
                       (apply-env-ref env id)
                       (eval-expression exp env))
                      1)]
                   [(equal? (apply-env-const env id) "unique-val")(eopl:error 'eval-expression
                                                                      "Attempt to assign an already define unique-val ~s")]
                   [else (eopl:error 'eval-expression
                                                                      "Attempt to assign a constant ~s")]
                    )
                )
      (app-exp (exp exps)
                (let ((proc (eval-expression exp env))
                     (args (eval-rands exps env)))
                 (if (procval? proc)
                     (apply-procedure proc args)
                     (eopl:error 'eval-expression
                                 "Attempt to apply non-procedure ~s" proc))))
      
      (long-exp (proc-names idss bodies long-body)
                  (eval-expression long-body
                                   (extend-env-recursively "const" proc-names idss bodies env)))
      (list-exp (exps)
                (if (null? (cdr exps)) (list (eval-expression (car exps) env))
                    (cons (eval-expression (car exps) env) (eval-expression (list-exp(cdr exps)) env))))
      (seq-exp (exp exps)
               (let loop ((acc (eval-expression exp env))
                             (exps exps))
                    (if (null? exps) acc
                        (loop (eval-expression (car exps) env)
                              (cdr exps)))))
      )))

;=====================================================================================
  
; Funciones auxiliares de eval-expression:


; (eval-rands, eval-let-exp-rand , eval-let-exp , eval-let-exp-rand):  Funciones auxiliares para aplicar eval-expression 
; a cada elemento de una lista de expresiones

(define eval-rands
  (lambda (rands env)
    (map (lambda (x) (eval-rand x env)) rands)))

(define eval-rand
  (lambda (rand env)
    (cases expression rand
      (id (id)
               (indirect-target
                (let ((ref (apply-env-ref env id)))
                  (cases target (primitive-deref ref)
                    (direct-target (expval) ref)
                    (indirect-target (ref1) ref1)))))
      (else
       (direct-target (eval-expression rand env))))))

(define eval-let-exp
  (lambda (rands env)
    (map (lambda (x) (eval-let x env))rands)))

(define eval-let
  (lambda (rand env)
    (direct-target (eval-expression rand env))))

  
;eval-ope-bin:  Evalua las operaciones binarias

(define eval-ope-bin
  (lambda (f-val ope s-val)
    (cases primitive-binary ope
      (add-prim () (+ f-val s-val)) 
      (subtract-prim () (- f-val s-val))
      (multiply-prim () (* f-val s-val))
      (module-prim () (modulo f-val s-val))
      (div-prim () (/ f-val s-val)))))

;eval-ope-un:  Evalua las operaciones unarias

(define eval-ope-un
  (lambda (ope val)
    (cases primitive-unary ope
      (incr-prim ()(+ val 1))
      (decr-prim ()(- val 1)))))
  
;eval-ope-bin-base:  Evalua las operasciones binarias con valores de distintas bases
  
(define eval-ope-bin-base
  (lambda (f-val ope s-val)
    (cases primitive-binary-base ope
      (add-prim-base () (+ f-val s-val))
      (subtract-prim-base() (- f-val s-val))
      (multiply-prim-base () (* f-val s-val)))))

;eval-ope-un-base:  Evalua las operaciones unarias con valores de distintas bases
  
(define eval-ope-un-base
  (lambda (ope val)
    (cases primitive-unary-base ope
      (incr-prim-base () (+ val 1))
      (decr-prim-base ()(- val 1)))))

;eval-ope-bin-bool:  Evalua las operaciones binarias con booleanos
  
(define eval-ope-bin-bool
  (lambda (f-val ope s-val)
    (cases primitive-binary-bool ope
      (smaller-than-prim ()(< f-val s-val))
      (bigger-than-prim() (> f-val s-val))
      (less-or-equal-prim () (<= f-val s-val))
      (bigger-or-equal-prim () (>= f-val s-val))
      (equal-prim () (equal? f-val s-val))
      (different-prim () (not (equal? f-val s-val)))
      (and-prim () (and f-val s-val))
      (or-prim () (or f-val s-val)))))
       
;eval-ope-un-bool:  Evalua las operaciones unarias con booleanos
  
(define eval-ope-un-bool
  (lambda (ope exp)
    (cases primitive-unary-bool ope
      (not-prim () (not exp)))))


;eval-pred-prim-list:  Evalua los predicados con listas
  
(define eval-pred-prim-list
  (lambda (pred l)
    (cases primitive-pred-list pred
      (empty?-prim () (null? l))
      (list?-prim () (list? l)))))

;eval-bin-prim-list:  Evalua las operaciones binarias con listas
  
(define eval-bin-prim-list
  (lambda (f-exp ope s-exp)
    (cases primitive-binary-list ope
      (cons-prim () (cons f-exp s-exp))
      (append-prim (append f-exp s-exp)))))

;eval-un-prim-list:  Evalua las operaciones unarias con listas
  
(define eval-un-prim-list
  (lambda (ope l)
    (cases primitive-unary-list ope
      (car-prim () (car l))
      (cdr-prim () (cdr l)))))

;eval-bin-prim-string:  Evalua la operacion binaria con strings
  
(define eval-bin-prim-string
  (lambda (s1 ope s2)
    (cases primitive-binary-string ope
      (append-string-prim (string-append s1 s2)))))

;eval-un-prim-string:  Evalua la operacion unaria con strings

(define eval-un-prim-string
  (lambda (ope s)
    (cases primitive-unary-string ope
      (length-prim () (string-length s)))))

;eval-base:  Hace la conversion de base

(define (eval-base id list-of-numbers)
  (if (null? list-of-numbers) 0
      (+ (car list-of-numbers) (* id (eval-base (cdr list-of-numbers))))))

;return-exp-const-type
  
(define return-exp-const-type
  (lambda (expression)
    (cases exp expression
      (constant (id exp)  "const")
      (unique-var-define (id exp) "unique-val")
      (unique-var-undefine (id) "unique-val" ))))

;exp-const
  
(define exp->const
  (lambda (exps)
    (if (null? exps) empty
        (cons (return-exp-const-type (car exps) ) (exp->const (cdr exps))))))

;funcion auxiliar exp->ids
  
(define return-exp-ids
  (lambda (expression)
    (cases exp expression
      (constant (id exp)id)
      (unique-var-define (id exp) id)
      (unique-var-undefine (id) id))))


;exp->ids
  
(define exp->ids
  (lambda (exps)
    (if (null? exps)empty
        (cons (return-exp-ids (car exps))(exp->ids (cdr exps))))))


;funcion auxiliar exp->vals
  
(define return-exp-vals
  (lambda (expression)
    (cases exp expression
      (constant (id exp)  exp)
      (unique-var-define (id exp) exp)
      (unique-var-undefine (id) (string "#undefine#") ))))

;exp-vals
  
(define exp->vals
  (lambda (exps)
    (if (null? exps)empty
        (cons (return-exp-vals (car exps))(exp->vals (cdr exps))))))

;apply-env-const
  
(define apply-env-const
  (lambda (env sym)
    (cases environment env
      (empty-env-record ()
                        (eopl:error 'apply-env-ref "No binding for ~s" sym))
      (extended-env-record (const syms vals env)
                           (let ((pos (rib-find-position sym syms)))
                             (if (number? pos)
                                 (return-const const pos)
                                 (apply-env-const env sym)))))))

;return-cost
  
(define return-const
  (lambda (l pos)
    (if (equal? pos 0) (car l)
        (return-const (cdr l) (- pos 1)))))

;=====================================================================================

; Ambientes


;empty-env:  Crea el dato ambiente vacio
  
(define empty-env  
  (lambda ()
    (empty-env-record))) 

;enviroment:  Crea el dato ambiente
  
(define-datatype environment environment?
  (empty-env-record)
  (extended-env-record
   (const (list-of string?))
   (syms (list-of symbol?))
   (vec vector?)
   (env environment?)))

;extend-env: Crea el dato de ambiente extendido

(define extend-env
  (lambda (const syms vals env)
    (extended-env-record const syms (list->vector vals) env)))

;extend-env-recursively: Crea el dato de ambiente extendido para procedimientos recursivos

(define extend-env-recursively
  (lambda (const proc-names idss bodies old-env)
    (let ((len (length proc-names)))
      (let ((vec (make-vector len)))
        (let ((env (extended-env-record (make-const-list len) proc-names vec old-env)))
          (for-each
            (lambda (pos ids body)
              (vector-set! vec pos (direct-target (closure ids body env))))
            (trimmer len) idss bodies)
          env)))))

;trimmer: Función que recotorna una lista de los números desde 0 hasta end  (number => list)

(define trimmer
  (lambda (end)
    (let loop ((next 0))
      (if (>= next end) '()
        (cons next (loop (+ 1 next)))))))

;apply-env , apply-env-ref: Buscan un símbolo en un ambiente

(define apply-env
  (lambda (env sym)
      (deref (apply-env-ref env sym))))

(define apply-env-ref
  (lambda (env sym)
    (cases environment env
      (empty-env-record ()
                        (eopl:error 'apply-env-ref "No binding for ~s" sym))
      (extended-env-record (const syms vals env)
                           (let ((pos (rib-find-position sym syms)))
                             (if (number? pos)
                                 (a-ref pos vals)
                                 (apply-env-ref env sym)))))))

;=====================================================================================

; Definicion referencia y blanco


(define-datatype target target?
  (direct-target (expval expval?))
  (indirect-target (ref ref-to-direct-target?)))

(define-datatype reference reference?
  (a-ref (position integer?)
         (vec vector?)))

;expval? 
(define expval?
  (lambda (x)
    (or (number? x) (procval? x) (boolean? x) (string? x) (list? x))))

;ref-to-direct-target?
(define ref-to-direct-target?
  (lambda (x)
    (and (reference? x)
         (cases reference x
           (a-ref (pos vec)
                  (cases target (vector-ref vec pos)
                    (direct-target (v) #t)
                    (indirect-target (v) #f)))))))

;=====================================================================================

; Referancias y blancos:


;deref
(define deref
  (lambda (ref)
    (cases target (primitive-deref ref)
      (direct-target (expval) expval)
      (indirect-target (ref1)
                       (cases target (primitive-deref ref1)
                         (direct-target (expval) expval)
                         (indirect-target (p)
                                          (eopl:error 'deref
                                                      "Illegal reference: ~s" ref1)))))))
;primitive-deref
(define primitive-deref
  (lambda (ref)
    (cases reference ref
      (a-ref (pos vec)
             (vector-ref vec pos)))))
;setref!
(define setref!
  (lambda (ref expval)
    (let
        ((ref (cases target (primitive-deref ref)
                (direct-target (expval1) ref)
                (indirect-target (ref1) ref1))))
      (primitive-setref! ref (direct-target expval)))))

;primitive-setfref!
(define primitive-setref!
  (lambda (ref val)
    (cases reference ref
      (a-ref (pos vec)
             (vector-set! vec pos val)))))


;=====================================================================================

; Funciones auxiliares encargadas de encontrar la posicinn de un simboloen la lista de
; símbolos dentro de un ambiente 


;rib-find-position
(define rib-find-position 
  (lambda (sym los)
    (list-find-position sym los)))

;list-find-position
(define list-find-position
  (lambda (sym los)
    (list-index (lambda (sym1) (eqv? sym1 sym)) los)))

;list-index
(define list-index
  (lambda (pred ls)
    (cond
      ((null? ls) #f)
      ((pred (car ls)) 0)
      (else (let ((list-index-r (list-index pred (cdr ls))))
              (if (number? list-index-r)
                (+ list-index-r 1)
                #f))))))

;=====================================================================================
; PRUEBAS


;Función utilizando condicional

(just-parse "declare (const $x=9 ,const $y=3){
 if($x) {($x*$y)} else {($x+$y)}}")

;Funcion que haya el area de un tiangulo

(just-parse "declare (val-define $AreaTriangulo=
function($a, $b){($a * $b)})
{[$AreaTriangulo 2,2]}")

;Función que halla el area de una esfera

(just-parse "declare (const $pi = 3.1416, val-define $AreaEsfera=
function($pi, $r){((4* $pi)* ($r * $r))})
{[$AreaEsfera $pi, 2]}")

;====================== Funciones usando listas ======================

;Append

(just-parse "declare (const $listaUno = list <'Hola'>, const $listaDos = list <'amigos'>)
{append ($listaUno $listaDos)}")

;Length

(just-parse "declare( const $listaL = list< 'estudiante2' , 5, 4 , x8[0 5 1]  >){ length( car $listaL ) }")

;====================== Funciones usando listas ======================

;Funcion factorial

(just-parse " long (const $factorial ($n){
if (*($n > 0)) {($n * [$factorial sub1($n)])}
else {1}}) <[$factorial 6 ]>")

;Funcion sumatoria de n

(just-parse "long (const $suma($n){
if (*($n > 0)){($n + [$suma sub1($n)])} else {0}})
<[$suma 7]>")



